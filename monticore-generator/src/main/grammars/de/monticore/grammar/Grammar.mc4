/*
***************************************************************************************
Copyright (c) 2015, MontiCore
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice,
this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice,
this list of conditions and the following disclaimer in the documentation and/or
other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors
may be used to endorse or promote products derived from this software
without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
"AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR
CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY,
OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
SUCH DAMAGE.
***************************************************************************************
*/
package de.monticore.grammar;

/** The grammar describes the MontiCore grammar in its own format 
*/
component grammar Grammar extends de.monticore.literals.Literals {

  /** Used for embedding actions */
  external Action;
  
  /** Used as Expression in syntactic predicates */
  external ExpressionPredicate;  
	
  /** Used for embedding concepts */  
  external MCConcept;
 
  /** A MontiCore grammar describes a context free grammar in an own format   
  @attribute name The name of this grammar
  @attribute supergrammar Supergrammars of this grammar
  @attribute GrammarOption Options for this grammar
  @attribute LexProd Lexical productions
  @attribute BinaryProd Binary productions
  @attribute ClassProd Class productions
  @attribute EnumProd Enumeration productions
  @attribute ExternalProd External productions
  @attribute InterfaceProd List of all interface productions of this grammar
  @attribute AbstractProd List of all abstract productions of this grammar
  @attribute ASTRule List of all ast rules of this grammar
  @attribute AssociationBlock List of all associations blocks of this grammars
  @attribute Concepts List of additional concepts
  */
  MCGrammar =
    ("package" package:(Name& || ".")+ ";")? 
  	(importStatements:MCImportStatement)*
    
    (["component"])? "grammar" Name       
      ("extends" supergrammar:(GrammarReference || ",")+ )?
      "{"
         (
           GrammarOption
         |
           LexProd
         |
           BinaryProd
         |
           EncodeTableProd
         |
           OffsetProd
         |
           ClassProd
         |
           EnumProd   
         |
           ExternalProd
         |
           InterfaceProd
         |
           AbstractProd
         |
           ASTRule
         |
           Concept
         | StartRule
         )*
      "}" ;
 
  MCImportStatement = "import" importList:(Name& || ".")+ ("." star:["*"])? ";" ;

  // One OptionBlock only 
  ast MCGrammar =
    ASTGrammarOption max=1
    ASTStartRule max=1 ;
   
 
  /** A GrammarReference references another grammar
  @attribute name Qualified name of the other grammar
  */
  GrammarReference =
    (Name& || ".")+  ;
   
  // ------------------------------------------------------------------------------
  // Options
   
  /** Options for a grammar
  @attribute followOption Expression that shall be realized by lexer rules
  @attribute genericOption Expression that shall be realized by lexer rules
  */        
  GrammarOption=
    "options" "{"
      (
        FollowOption
      |
      	HammerOption
      |
        AntlrOption
      )*
    "}" ;
    
  /** A FollowOption specifies an additional follow set for a production
  @attribute prodName name of the production  
  @attribute alt the follow set
  */ 
  FollowOption =
    "follow" prodName:Name Alt ";"; 


  /** A antlr option is an option that is passed on Anltr
  itself   
  @attribute name name of the option
  @attribute value optional value
  */ 
  AntlrOption =
    Name  ("=" value:Name |"=" value:String)? ; 
    
  /* Options for the MCHammerParser */
  HammerOption = 
  	"hammer" Name ";";
    
  StartRule = 
    "start" RuleReference ";";
    
  // ------------------------------------------------------------------------------
  // Productions
 
  /** Productions form the syntactical structure of the grammar
  */      
  interface Prod;
 
  ast Prod =
    Name ;
  
 
  /** ParserProduction are handled by the parser
  */
  interface ParserProd extends Prod;      
 
 // ------------------------------------------------------------------------------
 // Productions
 
  /** BinaryProd **/
  BinaryProd implements Prod =
    (["fragment"] | ["comment"]  )*
    ("binary") Name
    (
      (
        BinaryOption ("{" initAction:Action "}")? // Statements
      |
        "{" initAction:Action "}"
      )?
      "=" alts:(BinaryAlt || "|")+
      
      (":" ("{" endAction:Action  "}")? (variable:Name&
        ("->" type:( Name& || ".")+ (":" "{" block:Action "}" )? )? )? // Statements
      )? 
    )
    ";";
    
  /** OffsetProd **/
  OffsetProd implements Prod =
    (["local"])?
    ("offset") Name
    "="
    ruleName:Name
    ":"
    a:SignedIntLiteral
    "*x"
    sign:["+"|"-"]
    b:IntLiteral
    ";";
 
  EncodeTableProd implements Prod =
    "encodeTable" Name
    "="  
    "{" 
    ("options" "{" (encodeTableOptions:EncodeTableOption ";")*  "}")?
    encodeTableEntries:EncodeTableEntry ("," encodeTableEntries:EncodeTableEntry)* 
    "}"
    ";";
 
  /** A LexProd is formed by the keyword ident in the beginning followed by an option
  slash indicating that this LexProd is protected. The following name, options and
  symbol are handed to antlr directly
  @attribute Protected If true, this identifier is protected and call only be called by
                       other identifiers
  @attribute Name Name of this identifier, only uppercase letters should be used
  @attribute LexOption Options for lexer rule
  @attribute InitAction Init actions for lexer rule
  @attribute Alts List of alts for this lexer rule
  @attribute Variable Variable used for mapping
                      (if type != null) or standard type (if type == null)
  @attribute Type Type of the lexer tokens
  @attribute Block Block used if other than standard mapping shall be used
  */
  LexProd implements Prod =
    (["fragment"] | ["comment"]  )*
    ("token") Name
    (
      (
        LexOption ("{" initAction:Action "}")? // Statements
      |
        "{" initAction:Action "}"
      )?
      "=" alts:(LexAlt || "|")+
      
      (":" ("{" endAction:Action  "}")? (variable:Name&
        ("->" type:( Name& || ".")+ (":" "{" block:Action "}" )? )? )? // Statements
      )? 
    )
    ";";
    
  /** An Enumeration-Production
  @attribute name Name of the production
  @attribute constant List of all constants
  */
  EnumProd implements Prod =
    "enum" Name "=" (Constant || "|")+ ";";


  /** A ExternalProduction represents a hole in the grammar where another production
  can be plugged in
  @attribute name Name of the hole
  @attribute genericType External type used for this hole rule
  */
  ExternalProd implements Prod =                 
    "external" Name SymbolDefinition? GenericType? ";";
 
 
  /** An ASTInterfaceRule represents an explicit definition for an interface production
  and may overwrite the standard generated rule which is mentioned in the productions
  @attribute external True if this interface is programmed manually
  @attribute name Name of the production
  @attribute Parameter List of production parameters
  @attribute superInterfaceRule Super interfaces
  @attribute aSTSuperInterface Super interfaces abstract syntax only
  @attribute overrideStandard True if interfaces is relevant
  @attribute interfaces Ordered list of productions that are used for parsing
  */
  InterfaceProd implements ParserProd =
    "interface" Name SymbolDefinition?
    (
      "extends" superInterfaceRule:(RuleReference || "," )+
    |
      "astextends" aSTSuperInterface:(GenericType || ",")+
    )*
    ";";
 
 
  /** An ASTInterfaceRule represents an explicit definition for an interface rule and
  overwrites the standard generated rule which is mentioned in the rules
  @attribute name Name of the production
  @attribute Parameter List of production parameters
  @attribute superInterfaceRule Super interfaces
  @attribute aSTSuperInterface Super interfaces abstract syntax only
  @attribute superRule Super productions
  @attribute aSTSuperClass Super productions abstract syntax only
  @attribute overrideStandard True if interfaces is relevant
  @attribute interfaces Ordered list of productions that are used for parsing
  */
  AbstractProd implements Prod =
    "abstract" Name SymbolDefinition?
    (
      "extends" superRule:(RuleReference || ",")+
    |
      "implements" superInterfaceRule:(RuleReference || ",")+
    |
      "astextends" aSTSuperClass:(GenericType || ",")+
    |
      "astimplements" aSTSuperInterface:(GenericType || ",")+
    )*  ";";
    
     
  /** A rule represents a rule (production) in a context free grammar.
  @attribute name Name of the rule
  @attribute type Type this rule contributes to
  @attribute superrule Rule reference to a super rule
  @attribute superInterfaceRule Rule reference to a super interface rule
  @attribute aSTSuperClass Rule reference to a super rule used for abstract syntax only
  @attribute aSTSuperInterfacerule Rule reference to a super rule used for abstract
                                   syntax only
  @attribute superInterfaceRule Rule reference to a super interface rule
  @attribute ruleParameters List of formal Parameters handed to this rule
  @attribute action Action executed priot to the rule body
  @attribute alts List of alternatives representing the body of the rule
  */
  ClassProd implements ParserProd =
    Name SymbolDefinition?
    (
       "extends" superRule:(RuleReference || ",")+
    |
       "implements" superInterfaceRule:(RuleReference || ",")+
    |
       "astextends" aSTSuperClass:(GenericType || ",")+
    |
       "astimplements" aSTSuperInterface:(GenericType || ",")+
    )*
       
    ("{" Action "}")?    // Statements
    ("=" alts:(Alt || "|")+ )? ";";

  ast ClassProd =
    method public String toString() { return name; } ;
 
 
  /** Cardinality
  @attribute True if unbounded cardinality (min and max are meaningless)
  @attribute min Minimal cardinality
  @attribute max Maximal cardinality
  */
  Card =
   (
     Unbounded:["*"]
   |  
     "min" "=" min:Num_Int
     ("max" "=" ( max:Num_Int | max:"*"))?
   |  
     "max" "=" ( max:Num_Int | max:"*")    
   );
 
 
 
  /** A RuleReference refers to another production
  */   
  RuleReference  =
    SemanticpredicateOrAction? Name ;
     
  ast RuleReference =
    method public String getTypeName() {
      return getName(); 
    }
    method public boolean isExternal() { 
        return false;
    };
 
  // ------------------------------------------------------------------------------
  // Production body
 
     
  /** An alternative represents an alternative in a rule or block and contains
  (Rule)Components
  @attribute Components List of the rule components of this alternative
  */ 
  Alt =
    ([rightAssoc:"<rightassoc>"])? components:RuleComponent* ;


  /** RuleComponents are parts of the alternatives
  */
  interface RuleComponent;

  /** A NonTerminalSeparator is a shortcut for the description of list structures
  @attribute variableName Name for a variable binding (Note: Only one attribute of
                          VariableName and UsuageName may have a value)
  @attribute usageName Name for attribute
  @attribute name The name of the nonterminal
  @attribute separator The terminal as list separator
  @attribute plusKeywords True indicates that all keywords are allowed as well
  @attribute iteration Iteration of nonterminal 
  */
  NonTerminalSeparator implements RuleComponent =
    (variableName:Name& "=" | usageName:Name&  ":")?
    "(" Name (plusKeywords:["&"])? "||" separator:String ")" iteration:["*"|"+"];
   


  /** A block is something used in rules which is surrounded by ()
  @attribute Option options for this look like a non-greedy behavior
  @attribute Alts List of alternatives
  @attribute Iteration Iteration of this block
  */
  Block  implements RuleComponent =
    "("
    (
      (
        Option  ( "init" "{" initAction:Action "}" )? // Statements
      |
        "init" "{" initAction:Action "}"  // Statements
      )
      ":"
    )?
    alts:(Alt || "|")+ ")"
    (iteration:["?"|"*"|"+"])?; 
 
 
  /** Option values handed to Antlr
  @attribute optionValue Value handes to Antlr
  */ 
  Option =
    "options" "{" OptionValue+ "}";
 
 
  /** Key/Value pairs handed to Antlr
  @attribute key Key
  @attribute value Value
  */ 
  OptionValue =
    key:Name "=" value:Name ";" ;
 
 
  /** Reference to another rule
  @attribute variableName Name for a variable binding (Note: Only one attribute of
                          VariableName and UsuageName may have a value)
  @attribute usageName Name for attribute
  @attribute plusKeywords True indicates that all keywords are allowed as well
  @attribute iteration Iteration of nonterminal
  */    
  NonTerminal implements RuleComponent =
    (variableName:Name&? "=" | usageName:Name&?  ":")?
    Name ("@" referencedSymbol:Name)?
    (plusKeywords:["&"] | iteration:["?"|"*"|"+"] )*;
 
 
  interface ITerminal;
  
  ast ITerminal =
      Name;
 
  /** A Terminal is usually something like "," or "start" */
  Terminal implements RuleComponent, ITerminal =
    (variableName:Name& "=" | usageName:Name&  ":")?     
    name:String
    (iteration:["?"|"*"|"+"])*;

       
  /** Constants are constant string but lead to an attribute */
  Constant implements ITerminal =
    (humanName:Name& ":")? name:String ;
 
  /** constants are sth like keywords which one needs to know that they where there,
  e.g. public. In the ast-class they are reflected as int oder boolean isMethods  */
  ConstantGroup implements RuleComponent =
     (variableName:Name& "=" | usageName:Name&  ":")?
     "[" Constant ("|" Constant )* "]"
     (iteration:["?"|"*"|"+"])? ;
        
          
  /** End of file as EOF keyword */
  Eof implements RuleComponent =
    "EOF";
 
  /** MCAnything is a special lexer production which represents 
  */
  MCAnything implements RuleComponent =
    "MCA"; 
 
  Anything implements RuleComponent=
    "."; 
 
  /** Handed on as antlr action or predicate, realised by external JavaDSLParser */
  SemanticpredicateOrAction implements RuleComponent=
     "{" ExpressionPredicate "}" Predicate:["?"] 
     |
      "{" Action "}"
     ;
  
  
  // ------------------------------------------------------------------------------
  // Concept
 
 
  /** The grammar can be extended by using concepts
  @attribute name Name of the concept
  @attribute concept The concept itself
  */   
  Concept=
    "concept" Name& concept:MCConcept;
 
     
  // --------------------------------------------------------------------------------
  // AST
 
 
  /** A ASTClassgenWithinGrammar additional information for the class generation
  process
  @attribute
  @attribute type Type this rule refers to
  @attribute aSTSuperInterface Super interfaces abstract syntax only
  @attribute aSTSuperClass Super productions abstract syntax only
  @attribute methods Methods to be added to the class generation
  @attribute attributeInAST Attributes to be added to the class generation
  @attribute method Sons to be added to the generation
  */
  ASTRule =
     "ast" type:Name
    (
      "astextends" aSTSuperClass:(GenericType || ",")+
    |
      "astimplements" aSTSuperInterface:(GenericType || ",")+
    )*
    (
      "=" ( Method | AttributeInAST )*
    )? ";";
 

  /** A method to be added to a certain ASTClass
  @attribute public True if method is public
  @attribute private True if method is private
  @attribute protected True if method is protected
  @attribute final True if method is final
  @attribute static True if method is static
  @attribute return Qualified name of the return type of the method
  @attribute name Name of the method
  @attribute parameter List of the parameters
  @attribute exceptions Lit of the thrown exceptions
  @attribute body Body of the Method (mostly a JavaLazy-Object)
  */
  Method=
      "method"
      (["public"] | ["private"] | ["protected"])?
      (["final"])?
      (["static"])?
      returnType:GenericType Name  
      "(" (MethodParameter || ",")* ")"
      ("throws" exceptions:(GenericType || ",")+)?
      "{" body:Action "}" ; // Statements
 
 
  /** Formal parameter of a method
  @attribute type Qualified name of the type
  @attribute name Name of the parameter
  */         
  MethodParameter =
    type:GenericType Name;
 
 
  /** New Sons (=another ASTClass or String) or changed type (changed on difference to
  the automatic inference rules) in ASTClass
  @attribute name Name of the son
  @attribute genericType Name of the referring type
  @attribute Iterate True if more than one son can be added (=list)
  @attribute unordered Defines whether a collection is ordered or unordered
  @attribute card Cardinality
  @attribute derived True indicates that the attribute is derived as specified in body
  @attribute body Specification of derived attribute
  */
  AttributeInAST =   
    (Name  ":")?
    ([unordered:"<<unordered>>"])?
    GenericType
    Card?
    (["derived"] "{" body:Action "}")? ;  // Statements
 
 
  /**
  A GenericType represents an generic java type
  @attribute name List containing the name of the type
  @attribute genericType List of all type parameters
  @attribute dimension Array dimension of the type
  */
  GenericType  = 
    (Name& || ".")+
    ("<" (GenericType || ",")+  ">")?
    {_aNode.setDimension(0);} ("[" "]"
      {_aNode.setDimension(_aNode.getDimension()+1);} )*;
 
  ast GenericType =
    Dimension:int
    method public String toString() {
      return  de.monticore.grammar.HelperGrammar.printGenericType(this); 
    }
    method public String getTypeName() {
      return de.monticore.grammar.HelperGrammar.printGenericType(this); 
    }
    method public boolean isExternal() { 
        return true;
    };
   
  // ------------------------------------------------------------------------------
  // Lexer
   
  LexAlt =
    (lexComponents:LexComponent)*;
 
  interface LexComponent;
      
  LexBlock implements LexComponent =
    (negate:["~"])? "("
    (
      (
        option:LexOption ("init" "{" initAction:Action "}")? // Statements
      |
        "init" "{" initAction:Action "}" // Statements
      )
      ":"
     )?
     lexAlts:(LexAlt || "|")+ ")"
     (iteration:["?"|"*"|"+"])?; 
  
  LexCharRange implements LexComponent =
     (negate:["~"])? lowerChar:Char ".." upperChar:Char;
    
  LexChar implements LexComponent =
    (negate:["~"])? Char ;

  LexString implements LexComponent =
    string:String ;

  LexActionOrPredicate implements LexComponent = 
     "{" ExpressionPredicate "}" Predicate:["?"] 
     ;
          
  LexNonTerminal implements LexComponent =
    (variable:Name& "=")? Name ;
   
  LexSimpleIteration implements LexComponent =
    (LexNonTerminal | LexString | LexChar) iteration:["?"|"*"|"+"];
   
  LexOption =
    "options" "{" iD:Name "=" value:Name ";" "}";

  // ------------------------------------------------------------------------------
  // Symbol Table

  SymbolDefinition =
    "@!" symbolKind:Name?;
    
  // ------------------------------------------------------------------------------
  // Binary
  
  BinaryAlt =
    (binaryComponents:BinaryComponent)*;
 
  interface BinaryComponent;
      
  BinaryBlock implements BinaryComponent =
    (negate:["~"])? "("
    (
      (
        option:BinaryOption ("init" "{" initAction:Action "}")? // Statements
      |
        "init" "{" initAction:Action "}" // Statements
      )
      ":"
     )?
     binaryAlts:(BinaryAlt || "|")+ ")"
     (iteration:["?"|"*"|"+"])?; 
     
  BinaryLengthValue implements BinaryComponent, RuleComponent =
    (UInt8 | UInt16 | UInt32 | UInt64 | UBits) "->" repeat:BinaryComponent ; 
    
  BinaryNRepeat implements BinaryComponent, RuleComponent =
    uIntValue:IntLiteral "->" repeat:BinaryComponent ; 
          
  BinaryNonTerminal implements BinaryComponent =
    (variable:Name& "=")? Name ;
   
  BinarySimpleIteration implements BinaryComponent =
    (BinaryNonTerminal | UInt8 | UInt16 | UInt32 | UInt64 | Int8 | Int16 | Int32 | Int64 | UBits | Bits ) iteration:["?"|"*"|"+"];
   
  BinaryOption =
    "options" "{" iD:Name "=" value:Name ";" "}";
    
  BinaryLength implements BinaryComponent, RuleComponent = 
    ( UInt8 | UInt16 | UInt32 | UInt64 | UBits ) 
    ".L{"
    id:Name
    "}" ;
    
  BinaryData implements BinaryComponent, RuleComponent = 
    ( BinaryNonTerminal | UInt8 | UInt16 | UInt32 | UInt64 | Int8 | Int16 | Int32 | Int64 | UBits | Bits ) 
    ".D{"
    id:Name
    "}" ;
    
  Offset implements RuleComponent =
    ( UInt8 | UInt16 | UInt32 | UInt64 | UBits ) "#" (variable:Name& "=")? Name;
    
  UInt8 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "uint8" 
    ( ranged:["["]
      ( lowerUInt:IntLiteral ".." upperUInt:IntLiteral
      | lowerChar:Char ".." upperChar:Char
      | valueUInt:IntLiteral
      | valueChar:Char ) 
    "]")?
    ;
    
  UInt16 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "uint16"
    ( valued:["["]
      ( lower:IntLiteral ranged:[".."] upper:IntLiteral
      | value:IntLiteral )
    "]")?
    ;
    
  UInt32 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "uint32"
    ( valued:["["]
      ( lower:IntLiteral ranged:[".."] upper:IntLiteral
      | value:IntLiteral )
    "]")?
    ;
    
  UInt64 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "uint64"
    ( valued:["["]
      ( lower:IntLiteral ranged:[".."] upper:IntLiteral
      | value:IntLiteral )
    "]")?
    ;
    
  Int8 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "int8"
    ( valued:["["]
      ( lower:SignedIntLiteral ranged:[".."] upper:SignedIntLiteral
      | value:SignedIntLiteral )
    "]")?
    ;
    
  Int16 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "int16"
    ( valued:["["]
      ( lower:SignedIntLiteral ranged:[".."] upper:SignedIntLiteral
      | value:SignedIntLiteral )
    "]")?
    ;
    
  Int32 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "int32"
    ( valued:["["]
      ( lower:SignedIntLiteral ranged:[".."] upper:SignedIntLiteral
      | value:SignedIntLiteral )
    "]")?
    ;
    
  Int64 implements BinaryComponent, RuleComponent =
    (negate:["~"])? "int64"
    ( valued:["["]
      ( lower:SignedIntLiteral ranged:[".."] upper:SignedIntLiteral
      | value:SignedIntLiteral )
    "]")?
    ; 
    
  
    
  UBits implements BinaryComponent, RuleComponent =
    bits:["ubits1"|"ubits2"|"ubits3"|"ubits4"
    	 |"ubits5"|"ubits6"|"ubits7"|"ubits8"
    	 |"ubits9"|"ubits10"|"ubits11"|"ubits12"
    	 |"ubits13"|"ubits14"|"ubits15"|"ubits16"
    	 |"ubits17"|"ubits18"|"ubits19"|"ubits20"
    	 |"ubits21"|"ubits22"|"ubits23"|"ubits24"
    	 |"ubits25"|"ubits26"|"ubits27"|"ubits28"
    	 |"ubits29"|"ubits30"|"ubits31"|"ubits32"
    	 |"ubits33"|"ubits34"|"ubits35"|"ubits36"
    	 |"ubits37"|"ubits38"|"ubits39"|"ubits40"
    	 |"ubits41"|"ubits42"|"ubits43"|"ubits44"
    	 |"ubits45"|"ubits46"|"ubits47"|"ubits48"
    	 |"ubits49"|"ubits50"|"ubits51"|"ubits52"
    	 |"ubits53"|"ubits54"|"ubits55"|"ubits56"
    	 |"ubits57"|"ubits58"|"ubits59"|"ubits60"
    	 |"ubits61"|"ubits62"|"ubits63"|"ubits64"
    	 ]
    ( 
      valued:["["]
      ( lower:IntLiteral ranged:[".."] upper:IntLiteral
      | value:IntLiteral )
      "]"
    )?
    ; 
    
  Bits implements BinaryComponent, RuleComponent =
    bits:["bits1"|"bits2"|"bits3"|"bits4"
    	 |"bits5"|"bits6"|"bits7"|"bits8"
    	 |"bits9"|"bits10"|"bits11"|"bits12"
    	 |"bits13"|"bits14"|"bits15"|"bits16"
    	 |"bits17"|"bits18"|"bits19"|"bits20"
    	 |"bits21"|"bits22"|"bits23"|"bits24"
    	 |"bits25"|"bits26"|"bits27"|"bits28"
    	 |"bits29"|"bits30"|"bits31"|"bits32"
    	 |"bits33"|"bits34"|"bits35"|"bits36"
    	 |"bits37"|"bits38"|"bits39"|"bits40"
    	 |"bits41"|"bits42"|"bits43"|"bits44"
    	 |"bits45"|"bits46"|"bits47"|"bits48"
    	 |"bits49"|"bits50"|"bits51"|"bits52"
    	 |"bits53"|"bits54"|"bits55"|"bits56"
    	 |"bits57"|"bits58"|"bits59"|"bits60"
    	 |"bits61"|"bits62"|"bits63"|"bits64"
    	 ]
    ( 
      valued:["["]
      ( lower:SignedIntLiteral ranged:[".."] upper:SignedIntLiteral
      | value:SignedIntLiteral )
      "]"
    )?
    ; 

  EncodeTableEntry = 
    (
      char:Char 
      |
      string:String
    )
    "->"
    replacement:String
    ("options" "{" (encodeTableEntryOptions:EncodeTableEntryOption ";")* "}")?
  ;

    EncodeTableEntryOption = 
      ["caseInsensitive"] | ["ignoreWS"]
    ;

    interface EncodeTableOption;

    EncodeTableOptionPrefix implements EncodeTableOption = 
      "prefix" "=" prefix:String
    ;

    EncodeTableOptionSuffix implements EncodeTableOption = 
      "suffix" "=" suffix:String
    ;
    
} 
